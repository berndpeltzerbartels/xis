plugins {
    id 'java-library'
    id 'maven-publish'
    // id 'signing'
}

group = rootProject.ext.projectGroup
version = rootProject.ext.projectVersion

repositories {
    mavenCentral()
}


tasks.withType(JavaCompile) {
    sourceCompatibility = rootProject.ext.javaVersion.toString()
    targetCompatibility = rootProject.ext.javaVersion.toString()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(rootProject.ext.javaVersionInt)
    }
    withSourcesJar()
    withJavadocJar()
}

// Local variables for versions
var lombokVersion = rootProject.ext.libraryVersions.lombok
var reflectionsVersion = rootProject.ext.libraryVersions.reflections
var mockitoVersion = rootProject.ext.libraryVersions.mockito
var graalVmVersion = "23.0.1"
var tinylogVersion = "2.6.1"
var nashornVersion = "15.4"
var jacksonDatabindVersion = "2.14.1"
var junitVersion = rootProject.ext.libraryVersions.junit
var assertjVersion = rootProject.ext.libraryVersions.assertj

dependencies {

    // Local project dependencies
    implementation project(':xis-context')
    implementation project(':xis-util')
    implementation project(':xis-controller-api')
    implementation project(':xis-resources')
    implementation project(':xis-server')
    implementation project(':xis-adapter-spi')
    implementation project(':xis-gson')
    implementation project(':xis-javascript')
    implementation project(':xis-http-controller')
    api(project(':xis-security-common'))
    implementation project(':xis-authentication')
    implementation project(':xis-authentication-api')
    //implementation project(':xis-security:xis-login')

    // Annotation processors
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // Implementation dependencies
    implementation "org.projectlombok:lombok:$lombokVersion"
    implementation "org.reflections:reflections:$reflectionsVersion"
    implementation "org.mockito:mockito-core:$mockitoVersion"
    implementation "org.graalvm.js:js:$graalVmVersion"
    implementation "org.graalvm.js:js-scriptengine:$graalVmVersion"
    implementation "org.graalvm.truffle:truffle-api:$graalVmVersion"
    implementation "org.tinylog:tinylog-impl:$tinylogVersion"
    implementation "org.openjdk.nashorn:nashorn-core:$nashornVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"


    // Test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
}

test {
    maxParallelForks = 4
    useJUnitPlatform()
    afterTest { desc, result ->
        logger.quiet "${desc.className} - ${desc.name}: ${result.resultType}"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = project.name

            pom {
                name = 'XIS ' + project.name.capitalize()
                description = 'Module ' + project.name + ' of the XIS declarative web framework.'
                url = 'https://github.com/berndpeltzerbartels/xis'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'bernd'
                        name = 'Bernd Peltzer-Bartels'
                        email = 'bernd@xis.one'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/berndpeltzerbartels/xis.git'
                    developerConnection = 'scm:git:git@github.com:berndpeltzerbartels/xis.git'
                    url = 'https://github.com/berndpeltzerbartels/xis'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")
            credentials {
                username = project.findProperty("ossrhUsername") ?: ""
                password = project.findProperty("ossrhPassword") ?: ""
            }
        }
    }
}

/*
signing {
    sign publishing.publications.mavenJava
    useGpgCmd()
}
*/


tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:none', true)
    options.addStringOption('encoding', 'UTF-8')
}
